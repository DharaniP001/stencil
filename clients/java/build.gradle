buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'idea'
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version "4.21.0"
    id "com.google.protobuf" version "0.8.15"
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id "io.freefair.lombok" version "5.3.3.3"
}

group 'io.odpf'
version '5.0.0'

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.5.1'
    implementation group: 'com.google.guava', name: 'guava', version: '27.0.1-jre'
    implementation group: 'com.timgroup', name: 'java-statsd-client', version: '3.1.0'
    implementation group: 'org.aeonbits.owner', name: 'owner', version: '1.0.9'

    testImplementation group: 'junit', name: 'junit' , version: '4.12'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.18.0'
    testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '12.0-rc2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.1.0'
}

buildscript.repositories.each { repositories.add(it) }

protobuf {
    generatedFilesBaseDir = "$projectDir/src/test/generated"
    protoc {
        artifact = "com.google.protobuf:protoc:3.1.0"
    }
    generateProtoTasks {
        all().each { task ->
            task.generateDescriptorSet = true
            task.descriptorSetOptions.includeSourceInfo = false
            task.descriptorSetOptions.includeImports = true
            task.descriptorSetOptions.path = "$projectDir/src/test/resources/__files/descriptors.bin"
        }
    }
}

idea {
    module {
        testSourceDirs += file("$projectDir/src/generated/test/java")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def ossrhUsername = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : ''
def ossrhPassword = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : ''

publishing {
    publications {
        maven(MavenPublication) {
            pom {
                name = 'Stencil'
                description = 'Library to pull latest proto descriptors from a remote server'
                url = 'https://github.com/odpf/stencil'

                scm {
                    url = 'https://github.com/odpf/stencil.git'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'Data Engineering'
                        email = 'de@go-jek.com'
                    }
                }
                from(components.java)
                artifact(sourcesJar)
                artifact(javadocJar)
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

nexusPublishing {
    packageGroup = "io.odpf"
    repositories {
        sonatype {
            stagingProfileId = "9bf6aebae3df9b"
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}
